/**
 * Copyright (c) 2011 Ivan Vladimirov Ivanov (ivan.vladimirov.ivanov@gmail.com)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense,
 * and/or sell copies of the Software, and to permit persons to whom the
 * Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included
 * in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
 * OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
 * THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
 * OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
 * ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
 * OTHER DEALINGS IN THE SOFTWARE.
 */


class Keyboard {

  function void init() {
    return;
  }

  function char keyPressed() {
    return Memory.peek(24576);
  }

  function char readChar() {
    var char ch;
    var boolean done;

    do Output.printChar(0);

    let done = false;
    while(~done) {
      let ch = Keyboard.keyPressed();
      if(ch > 0) {
        let done = true;
      }
    }

    let done = false;
    while(~done) {
      if(Keyboard.keyPressed() = 0) {
        let done = true;
      }
    }

    do Output.backSpace();
    do Output.printChar(ch);
    
    return ch;
  }

  function String readLine(String message) {
    var char ch;
    var String line;

    do Output.printString(message);

    let line = String.new(64);
    while(true) {
      let ch = Keyboard.readChar();
      if(ch = 128) {
        do Output.backSpace();
        do Output.println();
        return line;
      }
      if(ch = 129) {
        do Output.backSpace();
        do Output.backSpace();
        do line.eraseLastChar();
      }
      else {
        do line.appendChar(ch);
      }
    }

    return line;
  }

  function int readInt(String message) {
    var String line;

    let line = Keyboard.readLine(message);
    return line.intValue();
  }
}

